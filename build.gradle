// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext{
        kotlin_version = "1.4.21"
        // local variables (use def)
        def androidx_test_version = '1.1.0'
        def appcompat_version = '1.1.0'
        def constraint_layout_version = '1.1.3'
        def espresso_version = '3.3.0'
        def junit_version = '4.12'
        def lifecycle_version = '2.1.0'
        def livedata_version = '2.2.0-rc02'
        def material_version = '1.0.0'

        def android_core_version = '1.3.2'
        def swipe_refresh_version = '1.1.0'
        def fragment_version = '1.2.5'

        libraries = [
                // Kotlin standard library
                "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",

                // Android core version
                "androidx.core:core-ktx:$android_core_version",

                // Android UI and appcompat
                "androidx.appcompat:appcompat:$appcompat_version",
                "androidx.fragment:fragment-ktx:$fragment_version",
                "com.google.android.material:material:$material_version",
                "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_version",
                "androidx.constraintlayout:constraintlayout:$constraint_layout_version",

                // ViewModel and LiveData
                "androidx.lifecycle:lifecycle-extensions:$lifecycle_version",
                "androidx.lifecycle:lifecycle-livedata-ktx:$livedata_version",
                "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version",
                "androidx.lifecycle:lifecycle-process:$lifecycle_version",
        ]

        librariesKapt = [
                "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version",
        ]

        testLibraries = [
                // Junit
                "junit:junit:$junit_version"
        ]

        androidTestLibraries = [
                "androidx.test.ext:junit:$androidx_test_version",
                "androidx.test.espresso:espresso-core:$espresso_version",
        ]
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}